use collections.map
use collections.list

type Set[T] = Map[T, Unit]

fn setEmpty[T]() -> Set[T] = mapEmpty()

fn setHas[T](s: Set[T], x: T) -> Bool = match mapGet(s, x):
  Some(_) -> true
  None -> false

fn setAdd[T](s: Set[T], x: T) -> Set[T] = mapPut(s, x, ())

fn setRemove[T](s: Set[T], x: T) -> Set[T] = mapRemove(s, x)

fn setSize[T](s: Set[T]) -> Int = length(s)

fn setToList[T](s: Set[T]) -> List[T] = mapKeys(s)

fn _setAddAll[T](s: Set[T], xs: List[T]) -> Set[T] = match xs:
  Nil -> s
  Cons(x, rest) -> _setAddAll(setAdd(s, x), rest)

fn setAddAll[T](s: Set[T], xs: List[T]) -> Set[T] = _setAddAll(s, xs)

fn setFromList[T](xs: List[T]) -> Set[T] = setAddAll(setEmpty(), xs)

fn setUnion[T](a: Set[T], b: Set[T]) -> Set[T] = _setAddAll(a, setToList(b))

fn _setKeepIfIn[T](a: Set[T], b: Set[T], xs: List[T]) -> Set[T] = match xs:
  Nil -> setEmpty()
  Cons(x, rest) -> match setHas(b, x):
    true -> setAdd(_setKeepIfIn(a, b, rest), x)
    false -> _setKeepIfIn(a, b, rest)

fn setIntersect[T](a: Set[T], b: Set[T]) -> Set[T] = _setKeepIfIn(a, b, setToList(a))

fn _setKeepIfNotIn[T](a: Set[T], b: Set[T], xs: List[T]) -> Set[T] = match xs:
  Nil -> setEmpty()
  Cons(x, rest) -> match setHas(b, x):
    true -> _setKeepIfNotIn(a, b, rest)
    false -> setAdd(_setKeepIfNotIn(a, b, rest), x)

fn setDiff[T](a: Set[T], b: Set[T]) -> Set[T] = _setKeepIfNotIn(a, b, setToList(a))

fn _setAllIn[T](xs: List[T], b: Set[T]) -> Bool = match xs:
  Nil -> true
  Cons(x, rest) -> match setHas(b, x):
    true -> _setAllIn(rest, b)
    false -> false

fn setIsSubset[T](a: Set[T], b: Set[T]) -> Bool = _setAllIn(setToList(a), b)

fn setEquals[T](a: Set[T], b: Set[T]) -> Bool = setIsSubset(a, b) and setIsSubset(b, a)
